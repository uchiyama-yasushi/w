OUTPUT_FORMAT ("elf32-h8300")
OUTPUT_ARCH (h8300:h8300sx)

/* For program loaded by monitor.  */

MEMORY {
	start_vector	: o = 0xff2000, l = 0x4		/* unused */
	exceptions	: o = 0xff2010, l = 0x8		/* invalid inst, trace */
	interrupts	: o = 0xff201c, l = 0x3e4	/* -0xff2400 */
	ram		: o = 0xff2400, l = 0x9c00	/* -0xffc000 */
}

SECTIONS {
	 _stack_start = 0xffc000;

	 .start_vector :
	 {
	 	 _VBR = .;
	 	 *(.vector)
	 } > start_vector

	 .exceptions :
	 {
		*(.exceptions)
	 } > exceptions

	 .interrupts :
	 {
		*(.interrupts)
	 } > interrupts

	 .text :
	 {
	 	 *(.text*)
		 *(.rodata*)
		 . = ALIGN (4);
	 } > ram

	 .data :
	 {
	 	 _data_start = .;
	 	 *(.data*)
		 _data_end = .;
	 } > ram

	 .bss :
	 {
	         _bss_start = .;
	      	 *(.bss*)
	 } > ram
	_bss_end = .;

	/DISCARD/ : { *(.*debug*) }
	/DISCARD/ : { *(.comment) }
}